plugins {
	id 'java'
	id 'java-library'
	id 'checkstyle'
	id 'com.bmuschko.docker-remote-api' version '9.3.2'
    id 'org.openrewrite.rewrite' version '7.20.0'
    id 'org.springframework.boot' version '3.5.6' apply false
    id 'io.spring.dependency-management' version '1.1.7' apply false
    id 'io.freefair.lombok' version "8.13.1" apply false
}

import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage
import com.bmuschko.gradle.docker.tasks.image.DockerPushImage
import org.springframework.boot.gradle.plugin.SpringBootPlugin

ext {
	registry = project.registry_local
}

repositories {
	mavenCentral()
}

dependencies {
    rewrite("org.openrewrite.recipe:rewrite-static-analysis:2.21.0")
}

checkstyle {
	toolVersion = '10.18.1'
}

subprojects {
	apply plugin: 'java'
	apply plugin: 'java-library'
	apply plugin: 'checkstyle'
	apply plugin: 'io.spring.dependency-management'
	apply plugin: 'io.freefair.lombok'

	dependencyManagement {
		imports {
			mavenBom SpringBootPlugin.BOM_COORDINATES
		}
	}

	repositories {
		mavenCentral()
	}

	java {
		sourceCompatibility = '17'
	}

	compileJava {
		options.compilerArgs += '-parameters'
	}

	compileTestJava {
		options.compilerArgs += '-parameters'
	}

	tasks.withType(JavaCompile).configureEach {
		options.encoding = 'UTF-8'
	}

	// Only apply Spring Boot plugin to boot module
	if (name == 'boot') {
		apply plugin: 'org.springframework.boot'
		bootJar {
			mainClass = 'com.marvin.Application'
			duplicatesStrategy(DuplicatesStrategy.EXCLUDE)
			archiveFileName = 'backend.jar'
		}
	} else {
		// Disable bootJar task for non-Spring Boot modules if it exists
		tasks.matching { it.name == 'bootJar' }.configureEach {
			it.enabled = false
		}
	}

	dependencies {

		if (name != 'common') {
			implementation project(':common')
		}

		// Only Spring Boot dependencies for modules that need them
		if (name in ['api', 'vocabulary', 'image-server', 'mental-arithmetic', 'plants']) {
			implementation 'org.springframework.boot:spring-boot-starter-webflux'
			implementation 'org.springframework.boot:spring-boot-starter-security'
		}

		// Common dependencies for modules that need Jackson/Spring annotations
		if (name in ['common', 'api', 'importer', 'camt', 'database', 'consul', 'plants', 'image-server', 'vocabulary', 'influxdb', 'uploader', 'mental-arithmetic']) {
			implementation 'org.springframework.boot:spring-boot-starter'
			implementation 'com.fasterxml.jackson.core:jackson-databind'
			implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'
			implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jdk8'
            implementation 'org.springframework.boot:spring-boot-starter-actuator'
        }

	}

	// Configure Checkstyle for each subproject
	checkstyle {
        toolVersion = "12.1.1"
		configFile = file("$rootDir/config/checkstyle/checkstyle.xml")
		maxWarnings = 0
	}
}

tasks.register('buildAdapterDockerImage', DockerBuildImage) {
	dependsOn ':boot:bootJar'

	inputDir = file("$project.rootDir")
	dockerFile = file("$project.rootDir/Dockerfile")
	images = ([registry + '/applications:latest'])
}

tasks.register('pushAdapterDockerImage', DockerPushImage) {
	dependsOn buildAdapterDockerImage

	images = new HashSet(["$registry/applications:latest"])
}

// Checkstyle task for all subprojects
tasks.register('checkstyleAll') {
	dependsOn subprojects.collect { "${it.path}:checkstyleMain" }
	description = 'Run Checkstyle on all subprojects'
	group = 'Verification'
}

rewrite {
    activeRecipe("org.openrewrite.staticanalysis.CodeCleanup")
    checkstyleConfigFile = file("$rootDir/config/checkstyle/checkstyle.xml")
}
